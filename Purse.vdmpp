class Purse is subclass of Platform

types

public UserId = Platform`AccountId;
public Price= Markets`Price;
public Balance = real;
public Asset = map Markets`Stocks to real;
public Equity = real;
public PurseAccount:: 
 id: UserId
 user: User
 asset : Asset 
 balance: Balance
 equity: Equity;

instance variables

public purse : PurseAccount;
  
functions

public GetEquity: Asset * Markets`Price -> real
GetEquity(asset, price)==
if asset= {|->}
  then 0
else let id in set dom asset in 
       asset(id) * price + GetEquity({id} <-: asset, price)      
measure card dom asset;

operations

private IncreaseBal: real ==> ()
IncreaseBal(sum)==
  purse.balance := purse.balance + sum
pre sum > 0;

private DecreaseBal: real ==> ()
DecreaseBal(min)==
  purse.balance := purse.balance - min
pre min > 0
  and purse.balance > min;

private IncreaseAsset: Markets`Stocks * real ==> ()
IncreaseAsset(stock, sum)==
(
dcl actual : real,
updated: map Markets`Stocks to real;

actual := purse.asset(stock);
updated := {stock |-> actual + sum};
purse.asset := purse.asset ++ updated;
)
pre stock in set dom purse.asset 
  and sum > 0;

private DecreaseAsset: Markets`Stocks * real ==> ()
DecreaseAsset(stock, min)==
(
dcl actual : real,
updated: map Markets`Stocks to real;

actual := purse.asset(stock);
updated := {stock |-> actual - min};
purse.asset := purse.asset ++ updated;
)
pre dom purse.asset inter {stock} <> {}
    and min > 0
      and purse.asset(stock) > min;

pure public GetBalance:() ==> real
GetBalance()== 
  return purse.balance;

pure public GetAsset:() ==> Asset
GetAsset()== 
  return purse.asset;

public Purse: Platform`AccountId * User`Name * User`Surname * User`Sex * User`DateBirth * User`Nationality * User`Country * User`Address ==> Purse
Purse(newId, nam, sur, s , db, nation, c, addr) ==
(
purse := mk_PurseAccount(newId,
                         new User (newId, nam, sur, s, db, nation, c, addr),
                         {|->},
                         0,
                         0);
) 

end Purse